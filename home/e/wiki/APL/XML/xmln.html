
<html>
<head>
<link rel="stylesheet" href="/public/assets/style.css" type="text/css">
<link rel="stylesheet" href=               "style.css" type="text/css">
</head>
<body><section>
<h1>xmln</h1>
<p><code>Index:<a href="../xml.html">XML</a></code></p>
</section>
<section class="function">
<p>Returns a JSON object from XML.</p>
<pre><code>      html ⍝ XHTML-compatible HTML
&lt;html&gt;
  &lt;body&gt;
    &lt;span style=&quot;font-weight:bold;&quot;&gt;Hello, World!&lt;/span&gt;
    &lt;h1&gt;Lorem,&lt;br /&gt;&lt;/h1&gt;
    &lt;h1&gt;Ipsum! &lt;/h1&gt;
  &lt;/body&gt;
&lt;/html&gt;
      
</code></pre>
<pre><code>      ⎕JSON xmln html
{
  &quot;html&quot;: {
    &quot;body&quot;: [
      {
        &quot;span&quot;: [
          &quot;Hello, World!&quot;,
          {
            &quot;style&quot;: &quot;font-weight:bold;&quot;
          }
        ]
      },
      {
        &quot;h1&quot;: {
          &quot;br&quot;: {},
          &quot;&quot;: &quot;Lorem,&quot;
        }
      },
      {
        &quot;h1&quot;: &quot;Ipsum!&quot;
      }
    ]
  }
}
</code></pre>
</section>
<section class="function">
<pre><code>xmln←{ ⍝ XML -&gt; JSON object
    obj arr char←1 2 4 ⍝ JSON types
    (d k v a t)←↓⍉¯1 ''⍬(0⍴'' '')1⍪⎕XML ⍵

    ⍝ pre-processing ; see: ]help ⎕XML ⎕JSON
    d+←1 ⋄ ((t∊1 3 7)/t)←obj ⋄ ((t∊5)/t)←char ⋄ (((⊂'')⍷v)/v)←⊂⍬ 
    I←{(⍺,⍵)[⍺⍺]}

    ⍝ Insert attributes as children
    idx←≢¨a
    i←⍋(⍳≢k),⍸idx
    (k v)←{⍵[i]}¨k v,¨↓⍉↑⊃,/↓¨(0&lt;idx)/a
    d←d(i I)d[⍸idx]+1
    t←obj char[1+0≠∊⍴¨v]

    ⍝ node pairs from depth vector
    N←{⊃,/2{↑{⊂⍺ ⍵}⌸⍨/↓⍉1↓⍵,∘(⌊/)⌸⍺}/↓⌈\(⍋⍵)@⊢(∪⍵)∘.=⍵}
    (p c)←↓⍉↑N d ⍝ parents | children

    ⍝ Insert object's character data as child
    idx←p∩⍸t=char
    i←⍋(⍳≢k),idx
    v←(((⊂⍬)@idx)v)(i I)v[idx]
    d←d(i I)d[idx+1]
    k←k(i I)(≢i)⍴⊂''
    t[idx]←arr ⍝ Objects with character data to type arr
    t←t(i I)(≢i)⍴⊂char

    (p c)←↓⍉↑N d

    ⍝ de-duplicate tree by changing parent type to arr
    msk←{0∊≠k[⍵]}¨c
    (t[msk/p])←arr
    idx←((∊(t[p]=arr)/c)∩⍸0≠∊⍴¨k)∪∊msk/c
    d[idx]+←1
    d[∊(p∊idx)/c]+←1
    i←⍋(⍳≢k),idx-1
    v←v(i I)(≢i)⍴⊂⍬
    d←d(i I)d[idx]-1
    k←k(i I)(≢i)⍴⊂''
    t←t(i I)(≢i)⍴1
    ⎕JSON 1 ⎕JSON⍠'M'⍉↑d k v t
}
</code></pre>
</section></body>
</html>

