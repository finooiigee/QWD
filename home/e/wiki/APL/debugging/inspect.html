
<html>
<head>
<link rel="stylesheet" href="/public/assets/style.css" type="text/css">
<link rel="stylesheet" href=               "style.css" type="text/css">
</head>
<body><section>
<h1>inspect</h1>
<hr>
<p><code>Index:<a href="../debugging.html">Debugging</a>|Presentation</code></p>
</section>
<section class="function">
<p>Returns the value of a variable that was used amidst a function's evaluation.</p>
<pre><code>      primes
primes ← {
    range    ← 1↓⍳⍵
    divisors ← (∪⊢∨⍳)¨range
    mask     ← 2=≢¨divisors
    mask/range
}

      primes 5
2 3 5

      (primes inspect 'divisors') 5
┌───┬───┬─────┬───┐
│1 2│1 3│1 2 4│1 5│
└───┴───┴─────┴───┘

</code></pre>
<p>This is not intended to be a helpful tool, instead it attempts to be useful in presentations - as a means to visualize an idea in focus. Additionally, it can be applied to view an operation in full:</p>
<pre><code>    names ← {(0≠∊⍴¨⍵)/⍵}{¯1↓((' '≠⍵)∧⌽∨\⌽'←'=⍵)/⍵}¨1↓⎕NR 'primes'
    names (↑,⍥⊂¨) (primes inspect names) 5
┌────────┬───────────────────┐
│range   │2 3 4 5            │
├────────┼───────────────────┤
│divisors│┌───┬───┬─────┬───┐│
│        ││1 2│1 3│1 2 4│1 5││
│        │└───┴───┴─────┴───┘│
├────────┼───────────────────┤
│mask    │1 1 0 1            │
└────────┴───────────────────┘

</code></pre>
<p>As it stands<sub>[19/04/2023]</sub> this function is limited in its simplicity, but I may improve upon it.</p>
</section>
<section class="function">
<pre><code>inspect←{
    ⍺←⊢ ⋄ operand←⍺⍺ ⋄ names←⊆⍵⍵
    _←names(⍎,)¨⊂'←⎕NULL'
    _←⎕FX (,names,¨'←')⎕R(,names,¨⊂'∘←')¨⎕NR'operand'
    _←⍺ operand ⍵
    ⍎∊names,¨' '
}
</code></pre>
</section></body>
</html>

