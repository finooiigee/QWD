
<html>
<head>
<link rel="stylesheet" href="/public/assets/style.css" type="text/css">
<link rel="stylesheet" href=               "style.css" type="text/css">
</head>
<body><section>
<h1>Notation</h1>
<pre><code>Ⓧ ← {{⊂⍺↓⍵}⌺⍺⊢⍵}            ⍝ Compositional stencil
⍙ ← {⍺←⊢ ⋄ ⍺(⍵⍵⍣¯1 ⍺⍺⍥⍵⍵)⍵} ⍝ Under: apply pre-processing with ⍵⍵ before main function ⍺⍺, then reverse pre-processing
</code></pre>
<h1>Debugging</h1>
<pre><code>∇ r←name (operand inspect) arguments
    ⎕SHADOW name
    ⍎name,'←⎕NULL'
    ⎕FX(name,'←')⎕R(name,'∘←')¨⎕NR'operand'
    {}operand arguments
    r←⍎name
∇
</code></pre>
<h1>Transformations</h1>
<pre><code>⍝ Idioms and transformations on hierarchies
</code></pre>
<p><code class="prompt">tree</code></p>
<pre><code>     A
┌─┬──┼──┬─┐
B C  D  E F
│  ┌─┼─┐
G  H I J
</code></pre>
<p><code class="prompt">pairs</code></p>
<pre><code>pairs ← {
   parents  ← ((1⊖⍵∊'┼│')/⍥,⍵)@(∊∘'┼│')⍵
   siblings ← {
       windows ← ,¨1 3 Ⓧ ⍵
       mask    ← (∊∘⎕A ∧⍥(∨/) ∊∘'┌─┬─┐')¨windows
       ⊃¨(mask/⍥,(⎕A∘∩)¨windows)@{mask}⍵
   }⍣≡parents
   slices ← (⍪2 2) Ⓧ 1↓siblings
   pairs  ← ⊃,/{⊂⍺,⊂⍵~' '}⌸/⍙↓¨slices
   (' '≠⊃¨pairs)/pairs
}
</code></pre>
<p><code class="prompt">'siblings' pairs inspect tree</code></p>
<pre><code>     A     
AAAAAAAAAAA
B C  D  E F
B  DDDDD   
G  H I J 
</code></pre>
<p><code class="prompt">pairs tree</code></p>
<pre><code>┌───────────────┬───────┬───────────┐
│┌─┬───────────┐│┌─┬───┐│┌─┬───────┐│
││A│┌─┬─┬─┬─┬─┐│││B│┌─┐│││D│┌─┬─┬─┐││
││ ││B│C│D│E│F││││ ││G││││ ││H│I│J│││
││ │└─┴─┴─┴─┴─┘│││ │└─┘│││ │└─┴─┴─┘││
│└─┴───────────┘│└─┴───┘│└─┴───────┘│
└───────────────┴───────┴───────────┘
</code></pre>
</section></body>
</html>

